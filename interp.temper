
let {...} = import("./parser.temper");
let {...} = import("./value.temper");

export class Env {
  public env: GlobalScope;
  public parser: Parser;
  var putCharBuf: ListBuilder<Int>;

  public constructor() {
    env = new GlobalScope(new MapBuilder<String, Value>());
    parser = new Parser("");
    putCharBuf = new ListBuilder<Int>();
  }
 
  public source(code: String): Void {
    parser.setState(code);
    var prog = parser.readDefs();
    compProgram(prog);
  }

  public call(name: String, args: Listed<Value>): Value {
    var func = env.lookup(name).asLambda();
    var res: Value = number(0);
    var cont = fn(v: Value): Void {
      res = v;
    };
    var envData = new MapBuilder<String, Value>();
    for (var i = 0; i < func.params.length; i++) {
      envData[func.params[i]] = args[i];
    }
    var localEnv = new LocalScope(func.scope, func.params, envData);
    func.body.run(localEnv, cont);
    runTodo();
    return res;
  }


  public compErr(): Runner {
    return new ErrRunner();
  }

  public compLambda(form: Form): Runner {
    var params = form.args[0].as<Form>().args.map(fn(s: Tree): String {
      return s.as<Ident>().repr;
    });
    var body = compTree(form.args[1]);
    return new LambdaRunner(params, body);
  }

  public compCall(form: Form): Runner {
    var funcName = form.args[0].as<Ident>().repr;
    var self = this;
    var args = form.args.slice(1, form.args.length).map(fn(t: Tree): Runner {self.compTree(t)});
    return new CallRunner(funcName, args);
  }

  public compForm(form: Form): Runner {
    match (form.form) {
      "call" -> compCall(form);
      "lambda" -> compLambda(form);
      else -> compErr();
    }
  }

  public compTree(tree: Tree): Runner {
    match (tree) {
      is Form -> compForm(tree.as<Form>());
      is Ident -> new LookupRunner(tree.as<Ident>().repr);
      is Number -> new ValueRunner(number(tree.as<Number>().repr));
      is Text -> new ValueRunner(string(tree.as<Text>().repr));
      else -> compErr();
    }
  }

  public flush(): Void {
    console.log(putCharBuf.join("", fn(codePoint: Int): String {
      return String.fromCodePoint(codePoint);
    }));
    putCharBuf = new ListBuilder<Int>();
  }

  public putchar(c: Int): Void {
    putCharBuf.add(c);
  }

  public compExternName(name: String): Define {
    var self = this;
    match (name) {
      "equal" -> fn(args: Listed<Value>, k: Cont): Void {
        if (args[0].asInt() == args[1].asInt()) {
          k(number(1));
        } else {
          k(number(0));
        }
      };
      "above" -> fn(args: Listed<Value>, k: Cont): Void {
        if (args[1].asInt() < args[0].asInt()) {
          k(number(1));
        } else {
          k(number(0));
        }
      };
      "pair" -> fn(args: Listed<Value>, k: Cont): Void {
        k(cons(args[0], args[1]));
      };
      "first" ->  fn(args: Listed<Value>, k: Cont): Void {
        k(args[0].asCons().first);
      };
      "second" ->  fn(args: Listed<Value>, k: Cont): Void {
        k(args[0].asCons().second);
      };
      "add" ->  fn(args: Listed<Value>, k: Cont): Void {
        k(number(args[1].asInt() + args[0].asInt()));
      };
      "sub" ->  fn(args: Listed<Value>, k: Cont): Void {
        k(number(args[1].asInt() - args[0].asInt()));
      };
      "mul" ->  fn(args: Listed<Value>, k: Cont): Void {
        k(number(args[1].asInt() * args[0].asInt()));
      };
      "div" ->  fn(args: Listed<Value>, k: Cont): Void {
        k(number(args[1].asInt() / args[0].asInt()));
      };
      "mod" ->  fn(args: Listed<Value>, k: Cont): Void {
        k(number(args[1].asInt() % args[0].asInt()));
      };
      "putchar" -> fn(args: Listed<Value>, k: Cont): Void {
        var c = args[0].asInt();
        if (c == 10) {
          self.flush();
        } else {
          self.putchar(c);
        }
        k(number(0));
      };
      "if" ->  fn(args: Listed<Value>, k: Cont): Void {
        var func = if (!args[0].isZero()) {
          args[1].asLambda()
        } else {
          args[2].asLambda()
        };
        func.body.run(func.scope, k);
      };
      else -> fn(args: Listed<Value>, k: Cont): Void {
        bubble();
      };
    }
  }

  public compExtern(form: Form): Void {
    var name = form.args[0].as<Form>().args[0].as<Ident>().repr;
    var params = form.args[1].as<Form>().args.map(fn(x: Tree): String {
      return x.as<Form>().args[0].as<Ident>().repr;
    });
    var localEnv = new LocalScope(env, params, new MapBuilder<String, Value>());
    var lambda = lambda(localEnv, params, new DefineRunner(compExternName(name)));
    env.add(name, lambda);
  }

  public compFunc(form: Form): Void {
    var name = form.args[0].as<Form>().args[0].as<Ident>().repr;
    var params = form.args[1].as<Form>().args.map(fn(x: Tree): String {
      return x.as<Form>().args[0].as<Ident>().repr;
    });
    var body = compTree(form.args[2]);
    var localEnv = new LocalScope(env, params, new MapBuilder<String, Value>());
    var lambda = lambda(localEnv, params, body);
    env.add(name, lambda);
  }

  public compTopForm(form: Form): Void {
    match (form.form) {
      "extern" -> compExtern(form);
      "func" -> compFunc(form);
      else -> compErr();
    }
  }

  public compProgram(tree: Tree): Void {
    var form = tree.as<Form>();
    for (var i = 0; i < form.args.length; i++) {
      compTopForm(form.args[i].as<Form>());
    }
  }
}